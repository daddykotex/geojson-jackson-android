apply plugin: 'com.android.library'
apply plugin: 'maven'
apply plugin: 'signing'

android {
    signingConfigs {
    }
    compileSdkVersion 21
    buildToolsVersion "21.1.2"
    defaultConfig {
        minSdkVersion 9
        targetSdkVersion 21
        versionCode 1
        versionName LIBRARY_VERSION

    }
    sourceSets {
        main.setRoot('src/main')
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    packagingOptions {
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/NOTICE'
    }
}

def siteUrl = 'https://github.com/daddykotex/geojson-jackson-android'
def gitUrl = 'git@github.com:daddykotex/geojson-jackson-android.git'


def isReleaseBuild() {
    return !LIBRARY_VERSION.contains("SNAPSHOT")
}

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives javadocJar
    archives sourcesJar
}

uploadArchives {
    configuration = configurations.archives
    repositories.mavenDeployer {
        beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

        snapshotRepository(url: sonatypeSnapshotRepo) {
            authentication(userName: sonatypeUsername,
                    password: sonatypePassword)
        }

        repository(url: sonatypeReleaseRepo) {
            authentication(userName: sonatypeUsername,
                    password: sonatypePassword)
        }

        pom.project {
            name "GeoJson Jackson for Android"
            packaging "aar"
            description "Android GeoJSON Library to use with Jackson"
            url siteUrl

            scm {
                url siteUrl
                connection "scm:git:" + gitUrl
                developerConnection "scm:git:" + gitUrl
            }

            licenses {
                license {
                    name "The Apache Software License, Version 2.0"
                    url siteUrl + "/blob/master/LICENSE"
                    distribution "repo"
                }
            }

            developers {
                developer {
                    id "daddykotex"
                    name "David Francoeur"
                }
            }
        }
    }
}

signing {
    required { isReleaseBuild() && gradle.taskGraph.hasTask("uploadArchives") }
    sign configurations.archives
}


dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    compile "com.fasterxml.jackson.core:jackson-databind:$jacksonVersion"
    compile "com.fasterxml.jackson.core:jackson-core:$jacksonVersion"
    testCompile "junit:junit:$junitVersion"
    testCompile "org.mockito:mockito-all:$mockitoAll"
}
